apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.aefyr.sai"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 43
        versionName "3.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        normal {
            dimension "version"
            buildConfigField "int", "DEFAULT_THEME", "0"
        }

        fdroid {
            dimension "version"
            applicationId "com.aefyr.sai.fdroid"
            buildConfigField "int", "DEFAULT_THEME", "6"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.github.aefyr:pseudoapksigner:1.6'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    implementation 'moe.shizuku.privilege:api:3.0.0-alpha10'
    implementation 'android.arch.lifecycle:common-java8:2.1.0'
    implementation 'com.google.android:flexbox:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation "com.github.aefyr.flexfilter:flexfilter:0.6-alpha"

    //Google services
    normalImplementation 'com.google.firebase:firebase-core:17.2.2'
    normalImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'
}

//Cancel google services/fabric plugins for fdroid flavor
gradle.taskGraph.beforeTask {
    String taskName = it.name.toLowerCase()
    if(taskName.contains("fdroid") && (taskName.contains("googleservices") || taskName.contains("fabric"))){
        println(String.format("Cancelling task \"%s\" due to fdroid build flavor", it.name))
        it.setEnabled(false)
    }
}

apply plugin: 'com.google.gms.google-services'
